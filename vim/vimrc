" vim:foldmethod=marker
set encoding=utf-8

" PLUGINS {{{
"
execute pathogen#infect()
call pathogen#helptags()

filetype plugin indent on
syntax enable

" Solarized {{{
set t_Co=256
set background=dark
let g:solarized_termcolors=256
colo solarized
" }}}

" Ctrlp {{{
let g:ctrlp_map = '<c-p>'
let g:ctrlp_cmd = 'CtrlP'
let g:ctrlp_working_path_mode = 'ra'
" }}}

" Clam {{{
let g:clam_winpos = 'bel'
" }}}

" Airline {{{
let g:airline_powerline_fonts=1
let g:airline_theme='zenburn'
" }}} (airline)

" }}} (PLUGINS)


" OPTIONS {{{
"
set expandtab                           " expand tabs to spaces
set tabstop=4                           " number of spaces to display for each tab character
set softtabstop=4                       " number of spaces a typing a tab counts for in editing
set shiftwidth=4                        " number of spaces to use per step of indent

set foldmethod=syntax                   " define folds using language syntax
set cindent                             " use c-syle indenting (see help)
set backspace=indent,eol,start          " allow backspace in all contexts

set nu                                  " show line numbers
set cursorline                          " highight line of current cursor
set colorcolumn=85                      " highlight column/s
set list                                " display whitespace characters
set listchars=tab:>\ ,eol:Â¬             " symbols with which to display whitespace
set scrolloff=3                         " always display three lines above/below cursor

set ruler                               " display line,col of cursor on statusbar
set showcmd                             " show command in progress on last line
set laststatus=2                        " display a statusbar on all windows

set hlsearch                            " highlight search matches
set incsearch                           " highlight matches as they're typed

set modeline                            " detect modelines
set modelines=1                         " how many lines to check for modelines
set ttyfast                             " use a 'fast' terminal connection
set lazyredraw                          " don't redraw window during macro execution
set splitright                          " open new vertical splits on the right
set splitbelow                          " open new horizontal splits on the bottom

set undofile                            " save undo history for each file
set undodir=~/.vim/tmp/undo//           " store undofiles in unobtrusive location
set dir=~/.vim/tmp/swap//               " store swapfiles in unobtrusive location

" Colors for cursorline & colorcolumn (ugly by default in almost all colorschemes)
hi ColorColumn ctermbg=Black guibg=lightgrey
hi CursorLine ctermbg=Black cterm=bold
" }}}


" FUNCTIONS {{{

" s:setInterpreter: Choose an interpreter command {{{
"   for the file to be called from the <leader>z " and <leader>Z keymaps
function! s:setInterpreter()

    let oldic=&ignorecase
    set ignorecase

    " filetype -> [ repl_command, exec_command ]
    let repls = {
        \ "python":   [ "!python -i %", "!python %" ],
        \ "bash":     [ "!bash --rcfile %", "!bash %" ],
        \ "sh":       [ "!bash --rcfile %", "!bash %" ],
        \ "haskell":  [ "!ghci %", "!runghc %" ],
        \ }

    if has_key(repls, &filetype)
        let b:repl = repls[&filetype][0]
        let b:exec = repls[&filetype][1]
    else
        let b:repl = "echo \"unknown interpretor for filetype (\" . &filetype . \")\""
        let b:exec = "echo \"unknown interpretor for filetype (\" . &filetype . \")\""
    endif

    let &ignorecase=oldic
endfunction " }}}

" s:setWinSize: sets the winheight, winminheight, and winwidth {{{
"   to values that make sense with the terminal size
function! s:adjustWinSize()
    " usable_height = (total lines) - (wincount statuslines) -  (command line)
    let usable_height = &lines - winnr('$') - 2
    set winwidth=20

    if winnr('$') == 1 || usable_height <= 30
        set winheight=1
        set winminheight=1
        return
    else
        set winheight=30
    endif

    " Try as I may, the relationship between the maximum value of winminheight
    " that vim will take without complaining, and the actual amount of lines
    " remaining, remains completely unclear. So we'll adjust down until we get
    " a yes
    let c = 10
    while c > 1
        try
            let &winminheight=c
            break
        catch /E36:/
            let c -= 1
        endtry
    endwhile
endfunction " }}}

" }}} (FUNCTIONS)


" KEYMAPS {{{

" Currently taken leader keys: <space>FMTVWZcflmptuvwxz
let mapleader=","
let maplocalleader=""

" FASTER NAVIGATION! {{{
" navigate windows with <c-{h,j,k,l}>
nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-h> <c-w>h
nnoremap <c-l> <c-w>l
" simplify navigation on wrapped lines
nnoremap j gj
nnoremap k gk
" Use + and - to resize current window
map + <C-W>+
map - <C-W>-
" }}}

" FASTER SEARCHING! {{{
" ctrlp buffer search
nnoremap <leader>p :CtrlPBuffer<cr>
" deactivate search highlighing
nnoremap <leader><space> :nohlsearch<cr>
" Use normal regex search by default
nnoremap / /\v
vnoremap / /\v
" }}}

" FASTER FOLDING! {{{
" these maps are for use only if foldmethod=marker
" open/close a fold on the next line
nnoremap <leader>f :execute "normal! o" . b:comment_prefix . "{{{"<esc>
nnoremap <leader>c :execute "normal! o" . b:comment_prefix . "}}}"<esc>
" open/close a fold at the end of the current line
nnoremap <leader>F :execute "normal! A " . b:comment_prefix . "{{{"<esc>
nnoremap <leader>C :execute "normal! A " . b:comment_prefix . "}}}"<esc>
" }}}

" FASTER MODE SELECTION! {{{
" normal mode with jk
inoremap jk <Esc>
" semicolon to enter command line
nnoremap ; :
" }}}

" COMMON EDITS, WITH SPEED! {{{
" Enter and S-Enter insert a line below/above cursor
map <CR> o<Esc>
" Swap the case of a word or line
nnoremap <leader>u bgUwA
inoremap <c-u> <esc>bgUwA
" Make strings easily
inoremap qv ""<esc>i
" Paste the current line to the one below it
nnoremap - :m+1<cr>
" }}}

" USEFUL TOGGLES! {{{
" Open/close folds with spacebar
nnoremap <space> za
" switch tabstop style
nnoremap <leader>t :set expandtab tabstop=4 shiftwidth=4 softtabstop=4<cr>
nnoremap <leader>T :set expandtab tabstop=8 shiftwidth=8 softtabstop=4<cr>
nnoremap <leader>m :set expandtab tabstop=2 shiftwidth=2 softtabstop=2<cr>
nnoremap <leader>M :set noexpandtab tabstop=4 softtabstop=4 shiftwidth=4<cr>
" turn of listchars
nnoremap <leader>l :set list!<cr>
" toggle wrapping (and display current wrapping status)
nnoremap <leader>W :setlocal wrap!<cr>:setlocal wrap?<cr>
" }}}

" INTERACT WITH THE WORLD! {{{
" Use %% to refer to directory of current file on command line
cnoremap %% <C-R>=expand('%:h').'/'<cr>
" open python repl
map <leader>x :!python<cr>
" open interpreter for file
map <leader>z :execute b:repl<cr>
map <leader>Z :execute b:exec<cr>
" edit .vimrc
nnoremap <leader>v mP:e $MYVIMRC<CR>
nnoremap <leader>V :so $MYVIMRC<CR>`P
" Save as sudo from nonsudo buffer
nnoremap <leader>w :w !sudo tee %<cr>
" }}} (interact...)

" }}} (KEYMAPS)


" AUTOCMDS {{{

" filetype detection corrections
augroup ft "{{{
    au!
    au BufNewFile,BufReadPost cakefile,Cakefile set filetype=coffee
    au BufNewFile,BufReadPost *.json set filetype=javascript
    au Bufnewfile,Bufreadpost *.m set filetype=objc
    au Bufnewfile,Bufreadpost *.md set filetype=markdown
augroup END "}}}

" Really, these are default settings, but they're guarded by the BufWinEnter
" event to prevent vim from complaining about window size when opened in a
" small terminal
augroup wsize "{{{
    au!
    au BufWinEnter * call s:adjustWinSize()
augroup END "}}}

augroup golang "{{{
    au!
    au FileType go iabbrev pkm package main
    au FileType go inoremap fcm func main() {<cr>}<esc>ko
augroup END "}}}

augroup html "{{{
    au!
    au FileType html inoremap abdoc <!DOCTYPE html>
    au FileType html inoremap abchar <meta charset="utf=8">
    au FileType html inoremap abjs <script type="text/javascript" src=""></script><esc>F"i
    au FileType html inoremap abcss <link rel="stylesheet" href="" /><esc>F"i
    au FileType html inoremap abtitle <title></title><esc>F<i
    au FileType html inoremap abhref <a href=""></a><esc>F"i
    au FileType html inoremap abhtml <html><CR></html><esc>ko
    au FileType html inoremap abhead <head><CR></head><esc>ko
    au FileType html inoremap abbody <body><CR></body><esc>ko
    au FileType html inoremap abdiv <div><CR></div><esc>ko
    au FileType html inoremap abform <form><CR></form><esc>ko
    au FileType html inoremap abspan <span><CR></span><esc>ko
    au FileType html inoremap abstyle <style><CR></style><esc>ko
    au FileType html inoremap abpa <p><CR></p><esc>ko
augroup END "}}}

augroup twospace "{{{
    au!
    au FileType html,javascript,ruby,coffee,jade set softtabstop=2
    au FileType html,javascript,ruby,coffee,jade set tabstop=2
    au FileType html,javascript,ruby,coffee,jade set shiftwidth=2
augroup END "}}}

augroup commentstyle "{{{
    au!
    au FileType python,ruby,sh,zsh,bash,coffee      let b:comment_prefix="# "
    au FileType c,cpp,java,javascript               let b:comment_prefix="// "
    au FileType haskell,lua                         let b:comment_prefix="-- "
    au FileType sql                                 let b:comment_prefix="; "
    au FileTYpe vim                                 let b:comment_prefix="\" "
augroup END "}}}

augroup misc "{{{
    au!
    " call setInterpreter once FileType is known
    au FileType * call s:setInterpreter()
    " indentation based folding for several langs
    au FileType coffee,ruby,lua set foldmethod=indent
    " deactivate colorcolumn for man page
    au FileType man set cc=0
augroup END "}}}
" }}}


" SPELLING {{{
iab functino function
" }}}


" LOCAL SETTINGS {{{
if filereadable(glob("~/.vimrc-local"))
    source ~/.vimrc-local
endif
" }}}
